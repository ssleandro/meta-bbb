#@TYPE: Machine
#@NAME: Ambientte
#@SOC: i.MX 7Dual
#@DESCRIPTION: Machine configuration for Ambiente - Based on Toradex Colibri iMX7 SOM (eMMC)
#@MAINTAINER: O.S. Systems Software <contato@ossystems.com.br>

MACHINEOVERRIDES =. "mx7:mx7d:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

MACHINE_FEATURES = "screen usbgadget usbhost ext2 touchscreen"

PREFERRED_PROVIDER_virtual/kernel = "linux-toradex-mainline"
PREFERRED_PROVIDER_virtual/dtb = "ambientte-kernel-devicetree-overlays"
KERNEL_DEVICETREE = "imx7d-colibri-emmc-eval-v3.dtb"
KERNEL_DEVICETREE_OVERLAY_PATH = "/boot/devicetree"
KERNEL_DEVICETREE_OVERLAY = "h-board-nvd-imx7.dtbo"

# The kernel lives in a seperate partition, don't deploy it in /boot/
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

UBOOT_MACHINE = "colibri_imx7_emmc_defconfig"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-toradex"
PREFERRED_RPROVIDER_u-boot-fw-utils = "libubootenv"
PREFERRED_PROVIDER_u-boot-default = "u-boot-updatehub-script"
PREFERRED_PROVIDER_u-boot-default-script = "u-boot-updatehub-script"
PREFERRED_PROVIDER_u-boot-mkimage-native = "u-boot-tools-native"
PREFERRED_PROVIDER_nativesdk-u-boot-mkimage = "nativesdk-u-boot-tools"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += " \
    kernel-devicetree \
    kernel-devicetree-overlays \
    kernel-image \
"

IMAGE_FSTYPES = "wic.bmap wic.xz tar.xz"
IMAGE_BOOT_FILES = "boot.scr-${MACHINE};boot.scr"
WKS_FILE_DEPENDS_append = " u-boot-default-script"
WKS_FILES = "${MACHINE}.wks"

# Qt5 settings
PREFERRED_PROVIDER_virtual/egl = "opengldummy"
PREFERRED_PROVIDER_virtual/libgles2 = "opengldummy"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND = "u-boot"
UPDATEHUB_DEVICE_IDENTITY = "primary-iface"
UPDATEHUB_FILESYSTEM_SUPPORT = "ext4"
UPDATEHUB_IMAGE_TYPE = "active/inactive"
UPDATEHUB_INSTALL_MODE = "tarball"
UPDATEHUB_RUNTIME_PACKAGES_append = "\
    u-boot-default-env \
    u-boot-default-script \
    updatehub-config-toradex \
"

# UpdateHub Bootscript variables
UPDATEHUB_BOOTSCRIPT_LOAD_A = "\
load mmc \${devnum}:2 \${kernel_addr_r} /boot/${KERNEL_IMAGETYPE}; \
load mmc \${devnum}:2 \${fdt_addr_r} /boot/${KERNEL_DEVICETREE}; \
fdt addr \${fdt_addr_r} && fdt resize 0x20000 && \
load mmc \${devnum}:2 \${loadaddr} ${KERNEL_DEVICETREE_OVERLAY_PATH}/${KERNEL_DEVICETREE_OVERLAY} && \
fdt apply \${loadaddr} \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B = "\
load mmc \${devnum}:3 \${kernel_addr_r} /boot/${KERNEL_IMAGETYPE}; \
load mmc \${devnum}:3 \${fdt_addr_r} /boot/${KERNEL_DEVICETREE}; \
fdt addr \${fdt_addr_r} && fdt resize 0x20000 && \
load mmc \${devnum}:3 \${loadaddr} ${KERNEL_DEVICETREE_OVERLAY_PATH}/${KERNEL_DEVICETREE_OVERLAY} && \
fdt apply \${loadaddr} \
"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A = "part uuid mmc \${devnum}:2 uuid"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B = "part uuid mmc \${devnum}:3 uuid"
UPDATEHUB_BOOTSCRIPT_BOOTARGS = "\
console=\${console},\${baudrate} root=PARTUUID=\${uuid} rootwait rw fsck.mode=force fsck.repair=yes \
"
UPDATEHUB_BOOTSCRIPT_BOOTCMD = "bootz \${kernel_addr_r} - \${fdt_addr_r}"
